metadata
    Title : Cabin State Machine Diagram
    Author : Leon Starr
    Document ID : mint.elevator3.td.6
    Modification date : October 20, 2025
    Version : 3.3.1
    Organization : Model Integration, LLC
    Copyright notice > MIT
    Organization logo > mint
domain Elevator Management
class Cabin
events
    Doors secure
    Prepare to move
    Already there
    New transfer
    Take out of service
    Arrived at floor
    Cabin stopping
    Change requested
    Try redirect
    Transport unavailable
    Transport in progress
    Go
--
state Changing destination( new dest: Level Name )
activity
    ~.Go to floor( dest floor: ^new dest )? {
        Cabin redirected( updated dest: ^new dest ) -> /R53/Transfer
        Change requested -> me
    }
transitions
    Change requested > MOVING
--
state READY TO GO
activity
    Ready to go -> /R53/Transfer
transitions
    Take out of service > OUT OF SERVICE
    Go > Requesting transport
--
state SECURING DOORS
activity
    my door .= /R4/Door
    my door.Lock requested.set
    Lock -> my door
transitions
    Doors secure > READY TO GO
    Take out of service > OUT OF SERVICE
--
state Are we already there?
activity
    /R53/Transfer.Destination floor == /R43/Shaft Level.Floor ?
        Already there -> : Prepare to move -> me
transitions
    Prepare to move > SECURING DOORS
    Already there > PICKUP DROPOFF
--
state PICKUP DROPOFF
activity
    Cabin at destination -> /R53/Transfer
    Unlock -> /R4/Door
    ! /R2/Shaft.In service ? Take out of service -> me
transitions
    New transfer > Are we already there?
--
state Set emergency stop( nearest safe stop : Level Name)
activity
    Cabin redirected( updated dest: ^nearest safe stop) -> /R53/Transfer
transitions
    Arrived at floor > OUT OF SERVICE
--
state OUT OF SERVICE
activity
    /R4/Door.Emergency hold.set
    Passenger open -> /R4/Door
--
state EMERGENCY BRAKING
activity
    Stop at nearest floor -> ~
transitions
    Cabin stopping > Set emergency stop
--
state MOVING
activity
    // Cabin in motion
transitions
    Take out of service > EMERGENCY BRAKING
    Arrived at floor > PICKUP DROPOFF
    Try redirect > Changing destination
--
state Requesting transport
activity
    ~.Go to floor( dest floor: /R53/Transfer.Destination floor ) ?
        Transport in progress -> : Transport unavailable -> me
transitions
    Transport unavailable > OUT OF SERVICE
    Transport in progress > MOVING
--
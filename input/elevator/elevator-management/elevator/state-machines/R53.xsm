metadata
    Title : R53 / Shaft Multiple Assigner State Machine Diagram
    Author : Leon Starr
    Document ID : mint.elevator3.td.10
    Modification date : May 12, 2019
    Version : 3.2.4
    Organization : Model Integration, LLC
    Copyright notice > MIT
    Organization logo > mint
domain Elevator Management
relationship R53 / Shaft
events
    Service requested
    No destination
    Transfer created
    Transfer completed
    Dest change evaluated
--
state NO TRANSFER
activity
    // Waiting for service request
transitions
    Service requested > Search for new destination
--
state Search for new destination
activity
    // The Cabin in this Shaft is stationary and has no Transfer
    cabin in shaft .= /R2/Cabin
    destination aslev .= cabin in shaft.Ping both ways()
    destination aslev? {
        Execute -> *Transfer(Tint: _dark) &R53 cabin in shaft, destination aslev
        Transfer created -> me
    } : No destination -> me
transitions
    No destination > NO TRANSFER
    Transfer created > TRANSFER IN PROGRESS
--
state TRANSFER IN PROGRESS
activity
    // Active transfer
transitions
    Transfer completed > Search for new destination
    Service requested > Search for alternate destination
--
state Search for alternate destination
activity
    cabin in shaft .= /R2/Cabin
    xfer .= cabin in shaft/R53/Transfer
    call in progress .= xfer/R53/Accessible Shaft Level/R49/Floor Service( Direction: cabin in shaft.Travel direction )

    fwd dest .= cabin in shaft.Ping( dir: cabin in shaft.Travel direction )
    fwd dest? destination aslev .= fwd dest :
        !call in progress? {
            // Search opposite the travel direction only if not servicing
            // a floor request in the current direction
            rev dest .= cabin in shaft.Ping( dir: Travel direction.opposite )
            rev dest? cabin in shaft.Travel direction.toggle
            // may be empty
            destination aslev .= rev dest
        }

    destination aslev and destination aslev.Floor != xfer.Destination floor?
        Dest change request( new dest: destination aslev.floor ) -> xfer :
        Dest change evaluated -> me
transitions
    Dest change evaluated > TRANSFER IN PROGRESS
--
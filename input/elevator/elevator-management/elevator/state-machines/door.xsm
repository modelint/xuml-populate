metadata
    Title : Door State Machine
    Author : Leon Starr
    Document ID : mint.elevator3.td.7
    Modification date : October 4, 2025
    Version : 3.4.3
    Organization : Model Integration, LLC
    Copyright notice > MIT
    Organization logo > mint
domain Elevator Management
class Door
events
    Door opened
    Passenger open
    Passenger close
    Open delay canceled
    Time to close
    Hold released
    Door closed
    Lock
    Unlock
    Door blocked
    Keep trying
    Cannot close door
--
state OPENING
activity
transitions
    Door opened > OPEN
    Passenger close > CLOSING
--
state OPEN
activity
    my Bank .= /R4/R2/Bank
    Blocked? delay = my Bank.Block clear time :
        delay = my Bank.Passenger load time
    Time to close -> me @delay
transitions
    Passenger close > Cancel open delay
    Time to close > HOLDING OPEN
--
state Cancel open delay
activity
    Time to close ->* me
    Open delay canceled -> me
transitions
    Open delay canceled > HOLDING OPEN
--
state HOLDING OPEN
activity
    !Held ? Hold released -> me
transitions
    Hold released > CLOSING
--
state CLOSING
activity
transitions
    Door closed > CLOSED
    Passenger open > OPENING
    Door blocked > Count block
--
state CLOSED
activity
    Close attempts.reset
    Blocked.unset
    Lock requested? Lock -> me
    // May be None
    Door closed -> /R4/R53/Transfer
transitions
    Lock > LOCKED
    Passenger open > OPENING
--
state LOCKED
activity
    // Tell the cabin that weâ€™re ready to go
    Lock requested.unset
    Doors secure -> /R4/Cabin
transitions
    Unlock > OPENING
--
state Count block
activity
    Close attempts.increment
    (Close attempts > /R4/R2/Bank.Max close attempts) ? Cannot close door -> me : {
        Blocked.set
        Keep trying -> me
    }
transitions
    Keep trying > OPENING
    Cannot close door > CANNOT CLOSE
--
state CANNOT CLOSE
activity
    Take out of service -> /R4/Cabin
--